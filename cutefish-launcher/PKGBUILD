# Maintainer: noone <>
pkgname=cutefish-launcher-git
pkgver=r87.a54d5db
pkgrel=1
pkgdesc="Cutefish Launcher"
arch=('x86_64')
url="https://github.com/cutefishos/launcher.git"
license=('GPL3')
groups=('cutefish-git')
depends=('qt5-declarative' 'kwindowsystem')
makedepends=('git' 'cmake' 'extra-cmake-modules' 'gcc' 'ninja')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("${pkgname%-git}::git+${url}"
	'remove_translation.patch')
md5sums=('SKIP'
	'5afb932f485da4066d58e442f3d08100')

prepare() {
        cd "$srcdir/${pkgname%-git}"

        # Apply patches
        git apply "$srcdir"/*.patch
}

pkgver() {
        cd "$srcdir/${pkgname%-git}"

	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	cd "$srcdir/${pkgname%-git}"
        mkdir -p build && cd build
        rm -f CMakeCache.txt
	cmake \
		-G Ninja \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		..
	cmake --build .
}

package() {
	cd "$srcdir/${pkgname%-git}"
        cd build
	DESTDIR="$pkgdir/" cmake --install .
}

